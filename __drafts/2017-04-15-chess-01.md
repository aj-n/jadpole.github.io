---
layout: post
title: "A Chess Game in Elm and Rust"
subtitle: "01 - Setting things up"
categories: tutorial
---

|:-----------------:|---------------|
| Elm version       | 0.18

It's been a while since I've written on this blog and, after removing a bunch of
deprecated articles, I figured that it was time to update the site. So here's an
idea: let's build an online Chess game, using Elm for the frontend, and Rust for
the WebSocket server that allows players to communicate with one another.

Before we get into all that server-stuff, we shall let two users play together
in the same browser window. This will allow us to setup our UI and to figure out
what kinds of messages our server should handle.

I'm assuming that you already _know_ some Elm &mdash; that you've skimmed through
the docs and taken a look at <span title="The Elm Architecture">TEA</span>.
However, you need not have written an application on your own and I hope that
this series will allow you to _get it_, so that you can work on your own games
once you're done with this one.

I also expect that Elm and Rust (with Cargo) were installed on your computer,
and that you can access those from the command line. Otherwise, you ain't gonna
go very far. ;-)


## Creating a new Elm project

Without further ado, let us create a new folder for our project. On my machine,
I call it `chess/`, but that's kind of arbitrary. From now on, I will designate
this "root" of our project by `/`. The `/client` directory shall contain our
frontend (in Elm), and must contain a `package.json` file that enumerates the
project's dependencies. To simplify our job later, I include everything that we
will need during the course of this series.

> As this series progresses, I may modify this list of dependencies. Hence, if
> you are faced with an error regarding some "module not found", you should
> come back here and check whether anything was added since.

Create a file at `/client/package.json` and insert the following:

```json
{
    "version": "1.0.0",
    "summary": "A simple client for a game of chess",
    "repository": "https://github.com/<nickname>/chess.git",
    "license": "BSD3",
    "source-directories": [
        "."
    ],
    "exposed-modules": [],
    "dependencies": {
        "elm-lang/core": "5.1.1 <= v < 6.0.0",
        "elm-lang/html": "2.0.0 <= v < 3.0.0",
        "elm-lang/websocket": "1.0.2 <= v < 2.0.0"
    },
    "elm-version": "0.18.0 <= v < 0.19.0"
}
```

Once this is done, open a terminal in the `/client` directory and install the
required dependencies:

```
$ elm-package install
```

Next comes `/client/Main.elm`, where we will bootstrap together the many states
of our application.

```elm
import Dict exposing (Dict)
import Html exposing (Html)
import Html.Attributes as HA
import Html.Events as HE
import List
import String


main =
    Html.beginnerProgram
        { model = initModel
        , update = update
        , view = view
        }



-- INITIALIZATION


initModel : Model
initModel =
    { ... }



-- MODEL


type alias Model =
    { ... }



-- UPDATE


type Msg
    = NOP


update : Msg -> Model -> Model
update msg model =
    case msg of
        NOP ->
            model



-- VIEW


view : Model -> Html Msg
view model =
    ...
```

As you might have noticed, there are ellipses at some critical points that stalls
compilation. Let's fix those! We do not yet care about user input: we must first
model our application and render the current state of the chessboard.

Let us now focus on the former.


## The model

We wish to describe a snapshot of the game, with such information as:

* Where are the pieces located?
* Who's turn is it?

Let's keep it simple:

```elm
type alias Model =
    { board : List (Location, Piece)
    , activePlayer : Color
    }


{-| The color of a piece and of the player that owns it.
-}
type Color
    = White
    | Black
```

We specify the location using the standard _rank & file_ approach, that is, we
assign coordinates `(x, y)` to every square on the board. Note that both players
must agree on the origin for things to run smoothly. We shall therefore associate
`(0, 0)` to `A1`, the bottom-left square as seen from the **whites'** perspective.

```elm
{-| The coordinates of a square on the board, represented by two integers
between 0 and 7 (inclusive). In chess-speak,

    ( column, row ) ~ ( file, rank )
-}
type alias Location =
    ( ColumnID, RowID )

type alias ColumnID = Int
type alias RowID = Int
```

All that is left to do is to specify the nature of a piece. Using Elm's sum
types once again:

```elm
type alias Piece =
    { kind : Kind
    , color : Color
    , alive : Bool
    }


type Kind
    = King
    | Queen
    | Bishop
    | Knight
    | Rook
    | Pawn
```

You might wonder why we've used a `List`, instead of a `Dict`, to enumerate the
pieces on the board, and why we've added an `alive` field to the `Piece` type.
Quite simply: _animations_. With this method, we'll be able to rely on Elm's
diffing mechanism to easily transition between different states. When we
conceive the server though, things will be different: you can expect a `HashMap`
to be our principal tool.

Well, what are we waiting for? Let's initialize that shit!

We shall create two board configurations, one will be the initial state of a
typical game, the other will be useful when we configure the possible movements
of our pieces. Let's start with the latter.

```elm
{-| Generate some configuration of the board that allows us to debug the moves
that are available to a piece of a given `Kind`.
-}
testBoard : Kind -> List (Location, Piece)
testBoard kind =
    [ (3, 3) => Piece kind White True
    , (1, 1) => Piece Pawn White True
    , (3, 5) => Piece Pawn Black True
    ]


(=>) : a -> b -> (a, b)
(=>) = (,)
```

For convenience, I've defined the `(=>)` operator, which allows us to create a
tuple in a way that is reminiscent of a dictionary and, I figured, states our
intent pretty well in this context.

<!-- TODO Explain that better or remove entirely? -->

You may also notice that we used `Piece` as a _function_, which is a nice Elm
shortcut. In some cases, there's a risk of accidentally swapping arguments
_at a distance_ when modifying the data type. However, since we every argument
has a different type, there's no chance of error here!

Now for the initial board! We could list every single piece, one by one, but
this would be rather dull and heck, why be explicit when Elm can do that for us!

```elm
{-| The initial state of the board whenever a game begins.
-}
initBoard : List (Location, Piece)
initBoard =
    let
        initRow rank color =
            List.indexedMap (\file kind -> (file, rank) => Piece kind color True)

        backRow =
            [ Rook, Knight, Bishop, King, Queen, Bishop, Knight, Rook ]
    in
        List.concat
            [ initRow 0 White backRow
            , initRow 1 White (List.repeat 8 Pawn)
            , initRow 6 Black (List.repeat 8 Pawn)
            , initRow 7 Black backRow
            ]
```

Now that every piece of data is ready, it's time to finally initialize our model:

```elm
initModel : Model
initModel =
    { board = initBoard
    , activePlayer = White
    }
```

Now, let's actually _see_ what we've built.


## The view

It's now time to render a static game board. Once we're done, we should get
something looking exactly like this:

![A board without interactivity](/images/chess-01.png)

We'll start
by writing down the _structure_ that we expect to build.

```elm
view : Model -> Html Msg
view model =
    Html.div
        [ HA.
        ]
```
